SET(LIBRARY_SRCS nifalcon_test_fw.c)
INSTALL(FILES nifalcon.h nifalcon_test_fw.h DESTINATION ${INCLUDE_INSTALL_DIR})

MACRO(MAKE_LIBNIFALCON_C_LIBRARY COMM_TYPE COMM_INCLUDE_DIRS COMM_LIBS)
  STRING(TOUPPER ${COMM_TYPE} UPPER_COMM_TYPE)
  SET(C_COMM_SRCS nifalcon_${COMM_TYPE}.c ${LIBRARY_SRCS})
  INCLUDE_DIRECTORIES(${COMM_INCLUDE_DIRS})
  FOREACH(C_COMM_LIB_TYPE SHARED STATIC)
	SET(C_COMM_LIB ${LIBNIFALCON_C_PREFIX}_${COMM_TYPE}_${C_COMM_LIB_TYPE})
	ADD_LIBRARY (${C_COMM_LIB} ${C_COMM_LIB_TYPE} ${C_COMM_SRCS})
 	SET_TARGET_PROPERTIES (${C_COMM_LIB} PROPERTIES COMPILE_FLAGS -D${UPPER_COMM_TYPE})
	SET_TARGET_PROPERTIES (${C_COMM_LIB} PROPERTIES OUTPUT_NAME ${LIBNIFALCON_C_PREFIX}_${COMM_TYPE})
	SET_TARGET_PROPERTIES (${C_COMM_LIB} PROPERTIES CLEAN_DIRECT_OUTPUT 1)
	SET_TARGET_PROPERTIES (${C_COMM_LIB} PROPERTIES VERSION ${LIBNIFALCON_VERSION})
	TARGET_LINK_LIBRARIES(${C_COMM_LIB} ${COMM_LIBS})
	IF(NOT WIN32)
	  INSTALL(TARGETS ${C_COMM_LIB} LIBRARY DESTINATION ${LIBRARY_INSTALL_DIR} ARCHIVE DESTINATION ${LIBRARY_INSTALL_DIR})
	ENDIF(NOT WIN32)
	IF(APPLE)
	  SET_TARGET_PROPERTIES(${C_COMM_LIB} PROPERTIES INSTALL_NAME_DIR ${LIBRARY_INSTALL_DIR})	  
	ENDIF(APPLE)
  ENDFOREACH(C_COMM_LIB_TYPE)
ENDMACRO(MAKE_LIBNIFALCON_C_LIBRARY)

IF(LIBFTD2XX_FOUND)
  MAKE_LIBNIFALCON_C_LIBRARY(ftd2xx ${LIBFTD2XX_INCLUDE_DIRS} ${LIBFTD2XX_LIBRARIES})
ENDIF(LIBFTD2XX_FOUND)

IF(LIBFTDI_FOUND)
  MAKE_LIBNIFALCON_C_LIBRARY(libftdi ${LIBFTDI_INCLUDE_DIRS} ${LIBFTDI_LIBRARIES})
ENDIF(LIBFTDI_FOUND)
